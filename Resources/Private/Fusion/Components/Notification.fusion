prototype(Breadlesscode.Notification:NotificationComponent) < prototype(Neos.Fusion:Component) {
    @context.config = ${ Configuration.setting('Breadlesscode.Notification') }
    # danger, warning or info
    type = ${ 'info' }
    content = ${ '' }
    onlyRenderInBackend = ${ false }
    # css classes
    baseClass = ${ config.classes.base }
    contentClass = ${ config.classes.content ? config.classes.content : this.baseClass + '__' + 'content' }
    # condition for only rendering in backend if onlyRenderInBackend is true
    @if.onlyBackend = ${ (this.onlyRenderInBackend && node.context.inBackend) || !this.onlyRenderInBackend }

    renderer = Neos.Fusion:Tag {
        attributes.class = Neos.Fusion:RawArray {
            baseClass = ${ props.baseClass }
            typeClass = ${ config.classes[props.type] ? config.classes[props.type] : props.baseClass + '--' + props.type }
        }
        content = Neos.Fusion:Tag {
            attributes.class = ${ props.contentClass }
            content = ${props.content}
        }
    }
}
